╔══════════════════════════════════════════════════════════════════════════════════════════════════╗
║                                               APL                                                ║
╟──────────────────────────────────────────────────────────────────────────────────────────────────╢
║                                                                                                  ║
║        1 │ w←⊃(⊃0⍴⍵){                           ⍝┌┌─2─┐       monadic; use ↓                     ║
║        2 │     (e a)←|⍺                         ⍝├ 0 0 1 1 1  dyadic; use /                      ║
║        3 │     T←⌽⍣(0>⊃⌽⍺)                      ⍝└──→⍺⍺←─────┐                                   ║
║        4 │     Pad←⍵⍵⍉(T⊣)⍪⍵⍪(T⊢)               ⍝ ┌⍺┐  ⌺     │                                   ║
║        5 │     need←(1+e),1↓⍴⍵                  ⍝ ┌─────⍵⍵──┐┘                                   ║
║        6 │     a=0:(1↓need⍴0↑⍵)Pad(1↓need⍴0↑⊢⍵) ⍝  0 0│1 2 3 4 5│0 0  Zero                       ║
║        7 │     a=1:(1↓need⍴1↑⍵)Pad(1↓need⍴1↑⊖⍵) ⍝  1 1│1 2 3 4 5│5 5  Replicate                  ║
║        8 │     a=2:(⊖¯1↓need⍴⊢⍵)Pad(¯1↓need⍴⊖⍵) ⍝  2 1│1 2 3 4 5│5 4  Reverse                    ║
║        9 │     a=3:(⊖⊢1↓need⍴⊢⍵)Pad(⊢1↓need⍴⊖⍵) ⍝  3 2│1 2 3 4 5│4 3  Mirror                     ║
║       10 │     a=4:(⊖¯1↓need⍴⊖⍵)Pad(¯1↓need⍴⊢⍵) ⍝  4 5│1 2 3 4 5│1 2  Wrap                       ║
║       11 │ }(¯1⌽⍳≢⍴⍵)/(⌽extra,¨⍺⊣0),⊂⍵          ⍝     └────⍵────┘                                ║
║                                                                                                  ║
╚══════════════════════════════════════════════════════════════════════════════════════════════════╝
